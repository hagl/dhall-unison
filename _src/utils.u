use support Date Time TimeZone
use support.Date Date
use support.Time Time
use support.TimeZone TimeZone
use syntax
use syntax URL
use syntax.URL URL
use syntax File
use syntax.File File
use syntax FilePrefix
use syntax.FilePrefix Absolute Here Home Parent
use syntax Scheme
use syntax.Scheme HTTP HTTPS
use syntax SHA256Digest
use syntax.SHA256Digest SHA256Digest
use syntax.ImportType Path Missing Remote Env
use syntax.TextLiteral Chunks
use syntax.ImportMode Code RawText Location
use syntax.Constant Type Kind Sort
use syntax Expression
use syntax.Expression Lambda Forall Let Variable If Merge ToMap EmptyList NonEmptyList Annotation Operator Application Field ProjectByLabels ProjectByType Completion Assert With DoubleLiteral NaturalLiteral IntegerLiteral TextLiteral RecordType RecordLiteral UnionType DateLiteral TimeLiteral TimeZoneLiteral Operator Builtin Constant DhallSome Import
use syntax.Operator Or And Equal NotEqual Plus Times TextAppend ListAppend CombineRecordTerms Prefer CombineRecordTypes Alternative Equivalent Some
use syntax.Builtin DhallNone

utils.escapeText: Text -> Text
utils.escapeText t =
  escape : Char -> [Char]
  escape = cases
    ?$ -> [?\\, ?u, ?0, ?0, ?2, ?4]
    ?" -> [?\\, ?"]
    ?\b -> [?\\, ?b]
    ?\f -> [?\\, ?f]
    ?\n -> [?\\, ?n]
    ?\r -> [?\\, ?r]
    ?\t -> [?\\, ?t]
    ?\\ -> [?\\, ?\\]
    c ->
      n = Char.toNat c
      if n Nat.< 32 then
        hexEncoding = Natural.toHex (Natural.fromNat n)
        Text.toCharList ((if n Nat.< 16 then "\\u000" else "\\u00") ++ hexEncoding)
      else [c]
  "\"" ++ Text.fromCharList (List.flatMap escape (Text.toCharList t)) ++ "\""

utils.splitOn: Text -> Text -> [Text]
utils.splitOn separator text =
  sep = Text.toCharList separator
  t = Text.toCharList text
  go : [Char] -> [Char] -> [[Char]] -> [[Char]]
  go l cur acc = match l with
    [] -> acc :+ cur
    (h +: t) -> match List.stripPrefix sep l with
      None -> go t (cur :+ h) acc
      Optional.Some l -> go l [] (acc :+ cur)
  List.map Text.fromCharList (go t [] [])

use Text

utils.printExpression: Expression -> Text
utils.printExpression = cases
  Lambda x A0 b0 -> "λ(" ++ x ++ ": " ++ (printExpression A0) ++ ") -> " ++ printExpression b0
  Forall x A0 B0 -> "∀(" ++ x ++ ": " ++ (printExpression A0) ++ ") -> " ++ printExpression B0
  Let x t a0 b0 ->
    ot = match t with
      Optional.Some T -> " : " ++ (printExpression T)
      None -> ""
    "let " ++ x ++ ot ++ " = " ++ (printExpression a0) ++ " in " ++ (printExpression b0)
  Variable x n -> if n Nat.== 0 then x else x ++ "@" ++ (Nat.toText n)
  If t0 l0 r0 -> "if " ++ (printExpression t0) ++ " then " ++ (printExpression l0) ++ " else " ++ (printExpression r0)
  Merge t0 u0 t -> "merge " ++ (printExpression t0) ++ " " ++ (printExpression u0) ++ (Optional.getOrElse "" (Optional.map (e -> " : " ++ printExpression(e)) t))
  ToMap t0 t -> "toMap " ++ (printExpression t0) ++ (Optional.getOrElse "" (Optional.map (e -> " : " ++ printExpression(e)) t))
  EmptyList T0 -> "[] : " ++ (printExpression T0)
  NonEmptyList ts0 -> "[" ++ (List.foldLeft (++) "" (List.intersperse ", " (List.map printExpression (Nonempty.toList ts0)))) ++ "]"
  Annotation t0 T0 -> (printExpression t0) ++ ": " ++ (printExpression T0)
  Operator l0 op r0 -> (printExpression l0) ++ (printOperator op) ++ (printExpression r0)
  Application f0 a0 -> (printExpression f0) ++ " " ++ (printExpression a0)
  Field t0 y -> "(" ++ (printExpression t0) ++ ")." ++ y
  ProjectByLabels t0 xs -> "(" ++ (printExpression t0) ++ ").{" ++ (List.foldLeft (++) "" (List.intersperse ", " xs)) ++ "}"
  ProjectByType t0 T0 -> "(" ++ (printExpression t0) ++ ").(" ++ (printExpression T0) ++ ")"
  Completion T0 r0 -> (printExpression T0) ++ "::" ++ (printExpression r0)
  Assert T0 -> "assert : " ++ (printExpression T0)
  With e0 ks v0 -> (printExpression e0) ++ " with " ++ (Text.join "." (List.map printPathComponent (Nonempty.toList ks))) ++ " = " ++ (printExpression v0)
  DoubleLiteral d -> Float.toText d
  NaturalLiteral n -> Natural.toDecimalText n
  IntegerLiteral n -> Integer.toText n
  TextLiteral (Chunks xys0 z) -> (List.foldLeft (++) "" (List.intersperse ", " (List.map (cases (t, ex) -> t ++ "{" ++ (printExpression ex) ++ "}") xys0))) ++ z
  RecordType ks0 -> "{" ++ (Text.join ", " (List.map (cases (a, e) -> a ++ ": " ++ (printExpression e)) ks0)) ++ "}"
  RecordLiteral ks0 -> "{" ++ (Text.join ", " (List.map (cases (a, e) -> a ++ " = " ++ (printExpression e)) ks0)) ++ "}"
  UnionType ks0 ->
    unionElements = List.map (cases
    (k, None) -> k
    (k, Optional.Some ex) -> k ++ ": " ++ (printExpression ex)) ks0
    "<" ++ (Text.join " | " unionElements)++ ">"
  DhallSome a0 -> "Some " ++ (printExpression a0)
  Builtin b -> printBuiltin b
  Constant c -> printConstant c
  DateLiteral (Date y m d) -> (Nat.toText y) ++ "-" ++ (Nat.toText m) ++ "-" ++ (Nat.toText d)
  TimeLiteral (Time h m s p) -> (Nat.toText h) ++ ":" ++ (Nat.toText m) ++ ":" ++ (Nat.toText (s / (pow 10  p)))
  TimeZoneLiteral (TimeZone minutes) ->
      positive = minutes Int.>= +0
      absMin = if positive then Int.truncate0 minutes else Int.truncate0 (Int.negate minutes)
      (if positive then "+" else "-") ++ (Nat.toText (absMin / 60)) ++ ":" ++ (Nat.toText (Nat.mod absMin 60))
  Import importType importMode optDigest -> (printImportType importType) ++ (printImportMode importMode) ++ (Optional.fold '("") (d -> " " ++ printDigest d) optDigest)

utils.printPathComponent : PathComponent -> Text
utils.printPathComponent = cases
  Label t -> t
  DescendOptional -> "?"

utils.printDigest : SHA256Digest -> Text
utils.printDigest = cases SHA256Digest bytes ->
  Text.fromCharList (List.filterMap Char.fromNat (Bytes.toList (Bytes.toBase64 bytes)))

utils.printFile : File -> Text
utils.printFile = cases File dirs file -> Text.join "/" (List.reverse (file +: dirs))

utils.printImportMode : ImportMode -> Text
utils.printImportMode = cases
  Code -> ""
  RawText -> " as Text"
  Location -> " as Location"

utils.printScheme : Scheme -> Text
utils.printScheme = cases
  HTTPS -> "https://"
  HTTP -> "http://"

utils.printFilePrefix : FilePrefix -> Text
utils.printFilePrefix = cases
  Absolute -> "/"
  Here -> "./"
  Parent -> "../"
  Home -> "~/"

utils.printImportType : ImportType -> Text
utils.printImportType = cases
  Missing -> "missing"
  Remote (URL scheme authority file optQuery) optHeaders -> (printScheme scheme) ++ authority ++ "/" ++ (printFile file) ++ (Optional.getOrElse "" optQuery) ++ (Optional.fold '("") (h -> " using " ++ (printExpression h)) optHeaders)
  Path prefix file -> (printFilePrefix prefix) ++ (printFile file)
  Env t -> "env:" ++ t

utils.printConstant : Constant -> Text
utils.printConstant = cases
  Type -> "Type"
  Kind -> "Kind"
  Sort -> "Sort"

utils.printBuiltin : Builtin -> Text
utils.printBuiltin =
  use syntax.Builtin Optional Natural Integer Double Text List Date Time TimeZone NaturalBuild NaturalFold NaturalIsZero NaturalEven NaturalOdd NaturalToInteger NaturalShow NaturalSubtract IntegerToDouble IntegerShow IntegerNegate IntegerClamp DoubleShow ListBuild ListFold ListLength ListHead ListLast ListIndexed ListReverse TextShow TextReplace Bool True False
  cases
    NaturalBuild -> "Natural/build"
    NaturalFold -> "Natural/fold"
    NaturalIsZero -> "Natural/isZero"
    NaturalEven -> "Natural/even"
    NaturalOdd -> "Natural/odd"
    NaturalToInteger -> "Natural/toInteger"
    NaturalShow -> "Natural/show"
    NaturalSubtract -> "Natural/subtract"
    IntegerToDouble -> "Integer/toDouble"
    IntegerShow -> "Integer/show"
    IntegerNegate -> "Integer/negate"
    IntegerClamp -> "Integer/clamp"
    DoubleShow -> "Double/show"
    ListBuild -> "List/build"
    ListFold -> "List/fold"
    ListLength -> "List/length"
    ListHead -> "List/head"
    ListLast -> "List/last"
    ListIndexed -> "List/indexed"
    ListReverse -> "List/reverse"
    TextShow -> "Text/show"
    TextReplace -> "Text/replace"
    Bool -> "Bool"
    Optional -> "Optional"
    Natural -> "Natural"
    Integer -> "Integer"
    Double -> "Double"
    Text -> "Text"
    List -> "List"
    True -> "True"
    False -> "False"
    DhallNone -> "None"
    Date -> "Date"
    Time -> "Time"
    TimeZone -> "TimeZone"

utils.printOperator : Operator -> Text
utils.printOperator = cases
  Or -> "||"
  Plus -> "+"
  TextAppend -> "++"
  ListAppend -> "#"
  And -> "&&"
  CombineRecordTerms -> "∧"
  Prefer -> "⫽"
  CombineRecordTypes -> "⩓"
  Times -> "*"
  Equal -> "=="
  NotEqual -> "!="
  Equivalent -> "=="
  Alternative -> "?"
